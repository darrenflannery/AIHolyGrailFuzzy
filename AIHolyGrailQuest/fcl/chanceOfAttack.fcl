/*
	Example: A chance of attack FIS (fuzzy inference system)
*/

FUNCTION_BLOCK chanceOfAttack	// Block definition (there may be more than one block per file)

VAR_INPUT				// Define input variables
	aggressiveness : REAL;
	observance : REAL;
END_VAR

VAR_OUTPUT				// Define output variable
	chanceOfAttack : REAL;
END_VAR

FUZZIFY aggressiveness			
	TERM calm := (0, 1) (4, 0) ; 
	TERM mild := (1, 0) (4,1) (6,1) (9,0);
	TERM strong := (6, 0) (9, 1);
END_FUZZIFY

FUZZIFY observance		
	TERM poor := (0, 1) (2, 1) (4,0) ;
	TERM good := (5,0) (7,1) (9,1);
END_FUZZIFY

DEFUZZIFY chanceOfAttack			
	TERM slim := (0,1) (15,1) (30,0);
	TERM average := (25,0) (40,1) (50,1) (65,0);
	TERM certain := (60,0) (75,1) (90,1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF aggressiveness IS calm OR observance IS poor THEN chanceOfAttack IS slim;
	RULE 2 : IF aggressiveness IS mild THEN chanceOfAttack IS average; 
	RULE 3 : IF aggressiveness IS strong AND observance IS good THEN chanceOfAttack IS certain;
END_RULEBLOCK

END_FUNCTION_BLOCK
