/*
	Example: A chance of attack FIS (fuzzy inference system)
*/

FUNCTION_BLOCK Attack	// Block definition (there may be more than one block per file)

VAR_INPUT				// Define input variables
	aggressiveness : REAL;
	accuracy : REAL;
END_VAR

VAR_OUTPUT				// Define output variable
	Attack : REAL;
END_VAR

FUZZIFY aggressiveness			
	TERM calm := (0, 1) (4, 0) ; 
	TERM mild := (1, 0) (4,1) (6,1) (9,0);
	TERM strong := (6, 0) (9, 1);
END_FUZZIFY

FUZZIFY accuracy		
	TERM poor := (0, 1) (2, 1) (4,0) ;
	TERM good := (5,0) (7,1) (9,1);
END_FUZZIFY

DEFUZZIFY Attack			
	TERM weak := (0,1) (10,1) (20,0);
	TERM average := (15,0) (20,1) (30,1) (35,0);
	TERM powerful := (30,0) (40,1) (50,1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF aggressiveness IS calm OR accuracy IS poor THEN Attack IS weak;
	RULE 2 : IF aggressiveness IS mild THEN Attack IS average; 
	RULE 3 : IF aggressiveness IS strong AND accuracy IS good THEN Attack IS powerful;
END_RULEBLOCK

END_FUNCTION_BLOCK
